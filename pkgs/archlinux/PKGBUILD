pkgname=enkei
pkgver=0.9.3
pkgrel=1
pkgdesc="A modern wallpaper tool with Gnome dynamic wallpaper support"
url="https://git.spacesnek.rocks/johannes/enkei"

arch=('x86_64' 'aarch64')
license=('GPL3')

makedepends=('cargo')
depends=('mesa' 'glib2' 'cairo' 'libwebp' 'wayland')

source=("$pkgname-$pkgver.tar.gz::https://git.spacesnek.rocks/johannes/$pkgname/archive/v$pkgver.tar.gz")
sha256sums=('40bfa515b9cb7e249648559e86ded2dd81d3d15479d78d56d330959b446aef13')

build() {
	cd "$pkgname"
	cargo build --release
}

package() {
	cd "$pkgname"
	install -Dm755 "target/release/$pkgname" "$pkgdir/usr/bin/$pkgname"
	install -Dm755 "target/release/${pkgname}ctl" "$pkgdir/usr/bin/${pkgname}ctl"
	install -Dm644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
